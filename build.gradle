plugins {
    id "java-library"
    id "maven-publish"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'idea'
}

group "dev.quantumfusion.dashloader"
version "1.5.2"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api libs.hyphen
    api libs.zstd.jni

    implementation libs.fastutil
    implementation libs.jetbrains.annotations
    implementation libs.gson
    implementation libs.apacheio
    implementation libs.taski

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.encoding = "UTF-8"
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        dashLoaderCore(MavenPublication) {
            from components.java
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}